{ inputs, ... }:
let
  system = "x86_64-linux";
  username = "andrew";
  stateVersion = "25.11";
  fontFamily = "CaskaydiaCove Nerd Font";

  mkSystem =
    hostName: modules:
    inputs.nixpkgs.lib.nixosSystem {
      inherit system;
      specialArgs = {
        inherit
          inputs
          username
          stateVersion
          fontFamily
          hostName
          ;
          self = inputs.self;
          utilsDir = ../../utils;
        };
      modules = [
        ../hosts/${hostName}/default.nix
        # inputs.nvf.nixosModules.default
        inputs.home-manager.nixosModules.home-manager
        {
          nixpkgs.pkgs = import inputs.nixpkgs {
            inherit system;
            overlays = [ (import inputs.rust-overlay) ];
            config.allowUnfree = true;
          };

          home-manager = {
            extraSpecialArgs = {
              inherit
                inputs
                username
                stateVersion
                fontFamily
                hostName
                ;
              self = inputs.self;
            };
            useGlobalPkgs = true;
            useUserPackages = true;
            users.${username} = import ../user/home.nix;
            backupFileExtension = "backup";
          };
        }
      ]
      ++ modules;
    };
in
{
  flake.nixosConfigurations = {
    andrew-pc = mkSystem "andrew-pc" [ ];

    andrew-laptop = mkSystem "andrew-laptop" [
      inputs.aic8800.nixosModules.default
    ];
  };
}
